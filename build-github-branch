import groovy.transform.TupleConstructor

@TupleConstructor()
class BranchOnRepo {
    String repo
    String branch
    String version
    String originalBranch = ""
}

def branchesToBuild = [
    new BranchOnRepo('apache/james-project', 'master', '3.3.0-SNAPSHOT'),
    new BranchOnRepo('apache/james-project', '3.1.x', '3.1.0', '3.1.x'),
    new BranchOnRepo('apache/james-project', '3.2.x', '3.2.0', '3.2.x'),
    new BranchOnRepo('linagora/james-project', 'openpaas-1.2.x', '3.2.0-SNAPSHOT', 'openpaas-1.2.x')
]

branchesToBuild.each {
    def branchOnRepo = it
    def repository = 'https://github.com/' + branchOnRepo.repo
   
    job("build ${branchOnRepo.repo} ${branchOnRepo.branch}".replaceAll(/\//, '.')) {
        githubProjectUrl(repository)
        scm {
            git {
                remote {
                    url(repository + '.git')
                }
                branch(branchOnRepo.branch)
            }
        }
        triggers {
            scm('* * * * *') {
                ignorePostCommitHooks(false)
            }
        }
        steps {
            downstreamParameterized {
                trigger('workflow', 'ALWAYS', false,
                    [buildStepFailure: 'FAILURE',
                     failure         : 'FAILURE',
                     unstable        : 'UNSTABLE']) {
                    predefinedProps([repoURL: repository, branch: branchOnRepo.branch, version: branchOnRepo.version, originalBranch: branchOnRepo.originalBranch])
                }
            }
        }
    }
}
